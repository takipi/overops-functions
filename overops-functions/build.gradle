apply plugin: 'eu.appsatori.fatjar'

archivesBaseName = 'overops-functions'

buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}

	dependencies {
		classpath 'eu.appsatori:gradle-fatjar-plugin:0.3'
	}
}

dependencies {
	compile (
		project(":overops-functions-shared"),
		"com.atlassian.jira:jira-rest-java-client-api:5.2.0",
		"com.atlassian.jira:jira-rest-java-client-core:5.2.0",
		"io.atlassian.fugue:fugue:4.7.2"
	)
}

fatJar {
	exclude 'META-INF/maven'
	exclude 'META-INF/*.example'
	manifest {
		attributes 'Main-Class': 'OveropsFunctions'
	}
}

uploadArchives {
	repositories {
		mavenDeployer {
			pom.project {
				name 'overops-functions'
				description 'OverOps Functions - Extending OverOps Reliability Platform'
				
				licenses {
					license {
						name 'MIT License'
						url 'http://www.opensource.org/licenses/mit-license.php'
					}
				}

				scm {
					connection 'scm:git:https://github.com/takipi/overops-functions/tree/master/overops-functions'
					developerConnection 'scm:git:https://github.com/takipi/overops-functions/tree/master/overops-functions'
					url 'https://github.com/takipi/overops-functions/tree/master/overops-functions'
				}
			}
		}
	}
}

task writeDependenciesFile() { 
	doLast {
		def resourcesDir = sourceSets.main.resources.srcDirs[0]
		def dependenciesFile = new File(resourcesDir, "META-INF/dependencies.txt")
		
		dependenciesFile.write(
			project.configurations.compile.resolve()
				.collect({ it.name })
				.join("\n")
		)
	}
}

processResources.dependsOn writeDependenciesFile

task copyJar(dependsOn: fatJar) {
	doLast {
		if (rootProject.hasProperty("jarTargetFile")) {
			println "Copying $jar.archivePath to $jarTargetFile"

			ant.copy(file:jar.archivePath, tofile:jarTargetFile)
		}
	}
}
